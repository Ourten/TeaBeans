buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'http://maven.ferenyr.info/artifactory/plugins-release'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
    }
}

apply plugin: 'java'

//IDE
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    jcenter()
}

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Maven Artefacts Info
group = "fr.ourten"
archivesBaseName = "teabeans"
version = "0.3.1"

dependencies {
	compile "com.google.guava:guava:19.0"
	testCompile "junit:junit:4.12"
    testRuntime "org.slf4j:slf4j-api:1.7.10"
}

// Publications

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = 'ourten'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications ('mavenJava')
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact (sourcesJar) { 
                classifier = 'sources'
            }
            artifact source: sourcesJar, classifier: 'sources', extension: 'jar'
        }
    }
}

apply plugin: "jacoco"

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
